plugins {
	id 'fabric-loom' version '1.9-SNAPSHOT'
	id 'maven-publish'
}

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

base {
	archivesName = project.archives_base_name
}

version = project.mod_version
group = project.maven_group

configurations {
    includeModImplementation
    includeImplementation

    include.extendsFrom includeImplementation
    include.extendsFrom includeModImplementation

    implementation.extendsFrom includeImplementation
    modImplementation.extendsFrom includeModImplementation
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// fabric api stuff
	includeModImplementation fabricApi.module("fabric-api-base", project.fabric_version)
	includeModImplementation fabricApi.module("fabric-command-api-v2", project.fabric_version)

    // gson, Google Json Java library
	// used by the main game
	implementation 'com.google.code.gson:gson:2.11.0'
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"

	it.options.release = 21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}_${project.minecraft_version}"}
	}
}